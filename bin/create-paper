#!/usr/bin/env zsh
#
# Usage: `create-paper <directory> <name>`
#
# Needs environment variables `FIGURE_SEARCH_DIRECTORIES` defined
# to collect figures and `BIBTEX_LIBRARY` to collect references.
#
# This currently installs python modules on every run, which
# increases predictabiliy at the expense of overwriting global
# state (this is *very* non-ideal). We should figure out if
# PYTHONPATH works here.
#
# Expects a specific structure of paper directory:
#
# text/
#   includes.yaml     Defines figures and tables
#   abstract.md
#   figure-captions.md
#   key-points.md
#   title-block.tex
#   references.bib    *This will be rewritten if BIBTEX_LIBRARY is defined*
#   chapters/         Contains body text, which will be read in order
# paper-components/   (the path to the repository containing this script...can probably be improved)
# build/              (this will be created)
# collected-figures/  (this will be created)
# output/             (also will be created)
#

cd "$1"
name="$2"

echo "Creating paper '$name' in directory $1"

build='build'
defs="paper-components/defs"
body="$build/body.tex"
bibfile="text/references.bib"
texfile="$build/draft.tex"
figureDefs="text/includes.yaml"
captions="$build/figure-captions.tex"
abstract="$build/abstract.tex"
build_dest="$build/$name.pdf"
auxfile="${build_dest:r}.aux"
outfile="output/$name.pdf"
collect_dir="collected-figures"

source paper-components/defs.zsh

pip install -e paper-components/modules/*

mkdir -p $build $collect_dir output

rm -f $body $captions $abstract

# Collect figures into centralized directory
figurator collect \
  $figureDefs \
  $collect_dir \
  ${FIGURE_SEARCH_DIRECTORIES}

## Figure captions
cat text/figure-captions.md \
| text-pipeline \
> $captions

# Build abstract
cat text/abstract.md \
| text-pipeline \
> $abstract

# Process body text
aggregate-text text/chapters/*.md  \
| sed -r 's/^\\section\{(Introduction)\}/\\invisiblesection\{\1\}/g' \
| mark-inline-figures \
| text-pipeline \
| figurator inline \
  text/includes.yaml \
  --template-dir paper-components/includes \
  --captions $captions \
  --collect-dir $collect_dir \
> $body

## Title block
cp text/title-block.tex build/title-block.tex


cp $defs/draft.tex $texfile

# Copy preamble
cat "$defs/preamble"/*.tex > "$build/preamble.tex"
# This needs to be abstracted because it
# is only for the `Orientation Statistics` paper.
cat text/math-defs.tex >> $preamble

cat text/key-points.md \
| text-pipeline \
> build/key-points.tex

echo "Running latex"
run-latex $texfile $build_dest #2>/dev/null

echo "Filtering bibliography"

# Filter bibliography
bib-filter --clean \
  --aux $auxfile $BIBTEX_LIBRARY $bibfile
# rebuild citations
bibtex $auxfile

echo "Running latex"
run-latex $texfile $build_dest
cp $build_dest $outfile

tag --set paper-draft $outfile

scale-images $outfile "${outfile:r}-sm.pdf" "printer" > /dev/null



